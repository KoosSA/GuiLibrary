plugins {
	id 'java-library'
	id 'maven-publish'
}

sourceCompatibility = '1.8'

group 'github.koossa'
version mavenPubVersion

project.ext {
	lwjglVersion = "3.3.2"
	jomlVersion = "1.10.+"
	lwjglNatives = "natives-windows"
	filesystem = "latest.release"
	savesystem = "latest.release"
	inputlib = "latest.release"
}

repositories {
	mavenCentral()
	
	maven { 
    	url = uri("https://maven.pkg.github.com/KoosSA/MavenRepos") 
    	credentials {
        	username = project.findProperty("gpr.user") ?: System.getenv("GIT_USERNAME")
        	password = project.findProperty("gpr.key") ?: System.getenv("GIT_TOKEN")
    	}
    }
}

dependencies {
	implementation platform("org.lwjgl:lwjgl-bom:$lwjglVersion")

	implementation "org.lwjgl:lwjgl"
	implementation "org.lwjgl:lwjgl-glfw"
	implementation "org.lwjgl:lwjgl-openal"
	implementation "org.lwjgl:lwjgl-opengl"
	implementation "org.lwjgl:lwjgl-stb"
	runtimeOnly "org.lwjgl:lwjgl::$lwjglNatives"
	runtimeOnly "org.lwjgl:lwjgl-glfw::$lwjglNatives"
	runtimeOnly "org.lwjgl:lwjgl-openal::$lwjglNatives"
	runtimeOnly "org.lwjgl:lwjgl-opengl::$lwjglNatives"
	runtimeOnly "org.lwjgl:lwjgl-stb::$lwjglNatives"
	
	implementation 'org.apache.commons:commons-lang3:3.14.0'
	implementation "org.joml:joml:${jomlVersion}"

	implementation "github.koossa:filesystem:$filesystem"
	implementation "github.koossa:savesystem:$savesystem"
	implementation "github.koossa:inputlibrary:$inputlib"
}

java {
    withSourcesJar()
    withJavadocJar()
}


publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/KoosSA/MavenRepos")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GIT_USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("GIT_TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
        	artifactId = 'guilibrary'
            from(components.java)
        }
    }
}